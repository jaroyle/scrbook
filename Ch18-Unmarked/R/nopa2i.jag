model{
#sigma ~ dunif(0, 500)
sigma ~ dgamma(0.001, 0.001)
#lam0 ~ dunif(0, 5)
lam0 ~ dgamma(0.001, 0.001)
#psi ~ dbeta(1,1)
psi ~ dbeta(0.001,1)
for(i in 1:M) {
   z[i] ~ dbern(psi)
   s[i,1] ~ dunif(xlim[1], xlim[2])
   s[i,2] ~ dunif(ylim[1], ylim[2])
   for(j in 1:J) { # Number of point count locations (J=105)
       distsq[i,j] <- (s[i,1] - X[j,1])^2 + (s[i,2] - X[j,2])^2
       lam[i,j] <- exp(-distsq[i,j] / (2*sigma^2)) * z[i]
       }
   }
for(j in 1:J) {
   bigLambda[j] <- lam0 * sum(lam[,j])
   for(k in 1:K) {
       n[j,k] ~ dpois(bigLambda[j])
       }
   }
N <- sum(z[])
A <- (xlim[2]-xlim[1])*(ylim[2]-ylim[1]) / 10000 # Area of state-space (ha)
D <- N / A      # Realized density
ED <- (M*psi)/A # Expected density
}
