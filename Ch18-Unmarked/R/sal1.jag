model {

p ~ dbeta(1,1)        # detection prob
tau ~ dunif(0, 5000)  # "movement parameter" of Gaussian kernel model
psi ~ dbeta(1,1)

for(i in 1:M) {
  z[i] ~ dbern(psi)
  s[i] ~ dcat(PrSeg[]) # which of the G segments is s in?
  for(g in 1:G) {
    PrU[i,g] <- exp(-distmat[s[i],g]^2/(2*tau^2)) # Pr(u(g) | s)
    }
  for(k in 1:K) {
    y[i,1:G,k] ~ dmulti(PrU[i,1:G], z[i])
    }
  }

for(j in 1:J) {
  for(k in 1:K) {
    N[j,k] <- sum(y[,seg[j],k]) # Number of individuals in seg j at time k
    n[j,1,k] ~ dbin(p, N[j,k])
    N2[j,k] <- N[j,k] - n[j,1,k]
    n[j,2,k] ~ dbin(p, N2[j,k])
    N3[j,k] <- N2[j,k] - n[j,2,k]
    n[j,3,k] ~ dbin(p, N3[j,k])
    }
  }

}