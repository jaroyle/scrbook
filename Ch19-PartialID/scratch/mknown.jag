model{

psi ~ dbeta(1,1)
omega ~ dbeta(1,1)

lam0 ~ dunif(0, 5)
sigma ~ dunif(0, 5)

for(i in 1:M) {
#  w[i] <- H[i]==1   # guy is marked. Data when m is known
  z[i] ~ dbern(psi)
  u[i] <- z[i]-w[i]   # guy is unmarked
  s[i,1] ~ dunif(xlim[1], xlim[2])
  s[i,2] ~ dunif(ylim[1], ylim[2])
  for(j in 1:J) {
    dist[i,j] <- sqrt((s[i,1]-X[j,1])^2 + (s[i,2]-X[j,2])^2)
    lambda[i,j] <- lam0*exp(-dist[i,j]^2/(2*sigma^2))
    for(k in 1:K) {
      # Model for (augmented) capture hist data for marked guys
      y[i,j,k] ~ dpois(lambda[i,j]*w[i]*z[i])
      # These are the latent cap histories for unmarked guys
      yu[i,j,k] ~ dpois(lambda[i,j]*u[i])
      }
    }
  }

for(j in 1:J) {
  for(k in 1:K) {
    nU[j,k] ~ dsum(yu[41,j,k],yu[42,j,k],yu[43,j,k],yu[44,j,k],yu[45,j,k],yu[46,j,k],yu[47,j,k],yu[48,j,k],yu[49,j,k],yu[50,j,k],yu[51,j,k],yu[52,j,k],yu[53,j,k],yu[54,j,k],yu[55,j,k],yu[56,j,k],yu[57,j,k],yu[58,j,k],yu[59,j,k],yu[60,j,k],yu[61,j,k],yu[62,j,k],yu[63,j,k],yu[64,j,k],yu[65,j,k],yu[66,j,k],yu[67,j,k],yu[68,j,k],yu[69,j,k],yu[70,j,k],yu[71,j,k],yu[72,j,k],yu[73,j,k],yu[74,j,k],yu[75,j,k],yu[76,j,k],yu[77,j,k],yu[78,j,k],yu[79,j,k],yu[80,j,k],yu[81,j,k],yu[82,j,k],yu[83,j,k],yu[84,j,k],yu[85,j,k],yu[86,j,k],yu[87,j,k],yu[88,j,k],yu[89,j,k],yu[90,j,k])
    }
  }

N <- sum(z[])
A <- (xlim[2]-xlim[1])*(ylim[2]-ylim[1])
D <- N/A
m <- sum(w[])
U <- sum(u[])

}