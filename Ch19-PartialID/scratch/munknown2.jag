model{

psi ~ dbeta(1,1)        # Pr(z=1), marginal prob
theta ~ dbeta(1,1)      # Pr(guy is marked, given that he is in B)
lam0 ~ dunif(0, 5)
sigma ~ dunif(0, 5)

for(i in 1:M) {
  s[i,1] ~ dunif(xlimS[1], xlimS[2])
  s[i,2] ~ dunif(ylimS[1], ylimS[2])
  inB[i] <- (s[i,1] > xlimB[1]) && (s[i,1] < xlimB[2]) &&
            (s[i,2] > ylimB[1]) && (s[i,2] < ylimB[2])
  cpi[i,1] <- inB[i]*theta*psi      # In B, marked
  cpi[i,2] <- inB[i]*(1-theta)*psi  # In B, unmarked
  cpi[i,3] <- (1-inB[i])*psi        # Outside B, unmarked
  cpi[i,4] <- 1 - sum(cpi[i,1:3])         # Fake guy
  h[i] ~ dcat(cpi[i,1:4]) # h=1 is data for *known* marked guys
  w[i] <- h[i] == 1 # guy is marked
  z[i] <- h[i] != 4 # guy is real if in states 1-3
  for(j in 1:J) {
    dist[i,j] <- sqrt((s[i,1]-X[j,1])^2 + (s[i,2]-X[j,2])^2)
    lambda[i,j] <- lam0*exp(-dist[i,j]^2/(2*sigma^2))
    for(k in 1:K) {
      # Model for (augmented) capture hist data for marked guys
      y[i,j,k] ~ dpois(lambda[i,j]*w[i]*z[i])
      # Latent capture histories for unmarked guys
      yu[i,j,k] ~ dpois(lambda[i,j]*(1-w[i])*z[i])
      }
    }
  }

for(j in 1:J) {
  for(k in 1:K) {
    nU[j,k] ~ dsum(yu[13,j,k],yu[14,j,k],yu[15,j,k],yu[16,j,k],yu[17,j,k],yu[18,j,k],yu[19,j,k],yu[20,j,k],yu[21,j,k],yu[22,j,k],yu[23,j,k],yu[24,j,k],yu[25,j,k],yu[26,j,k],yu[27,j,k],yu[28,j,k],yu[29,j,k],yu[30,j,k],yu[31,j,k],yu[32,j,k],yu[33,j,k],yu[34,j,k],yu[35,j,k],yu[36,j,k],yu[37,j,k],yu[38,j,k],yu[39,j,k],yu[40,j,k],yu[41,j,k],yu[42,j,k],yu[43,j,k],yu[44,j,k],yu[45,j,k],yu[46,j,k],yu[47,j,k],yu[48,j,k],yu[49,j,k],yu[50,j,k],yu[51,j,k],yu[52,j,k],yu[53,j,k],yu[54,j,k],yu[55,j,k],yu[56,j,k],yu[57,j,k],yu[58,j,k],yu[59,j,k],yu[60,j,k],yu[61,j,k],yu[62,j,k],yu[63,j,k],yu[64,j,k],yu[65,j,k],yu[66,j,k],yu[67,j,k],yu[68,j,k],yu[69,j,k],yu[70,j,k],yu[71,j,k],yu[72,j,k],yu[73,j,k],yu[74,j,k],yu[75,j,k],yu[76,j,k],yu[77,j,k],yu[78,j,k],yu[79,j,k],yu[80,j,k],yu[81,j,k],yu[82,j,k],yu[83,j,k],yu[84,j,k],yu[85,j,k],yu[86,j,k],yu[87,j,k],yu[88,j,k],yu[89,j,k],yu[90,j,k],yu[91,j,k],yu[92,j,k],yu[93,j,k],yu[94,j,k],yu[95,j,k],yu[96,j,k],yu[97,j,k],yu[98,j,k],yu[99,j,k],yu[100,j,k],yu[101,j,k],yu[102,j,k],yu[103,j,k],yu[104,j,k],yu[105,j,k],yu[106,j,k],yu[107,j,k],yu[108,j,k],yu[109,j,k],yu[110,j,k],yu[111,j,k],yu[112,j,k])
    }
  }

N <- sum(z[])
A <- (xlimS[2]-xlimS[1])*(ylimS[2]-ylimS[1])
D <- N/A
EN <- psi*M
ED <- EN/A

m <- sum(w[])
uInB <- sum(h==2)
uOutB <- sum(h==3)

}